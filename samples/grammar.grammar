# Grammar

grammar-file: ruleset*

ruleset: comment newline+ (rule|newline)+

rule: identifier ":" expr-choice


# Expression

expr-choice: expr-sequence ("|" expr-sequence)*

expr-sequence: expr-negate+

expr-negate: "~"? expr-quantified

expr-quantified: expr-group quantifier?

expr-group: "(" expr-choice ")" | expr-primary

expr-primary: literal-string | regex | identifier





# Lexical Grammar



newline:  [ \t\n]*[\n]

comment: "#" ~[\n]

identifier: [a-zA-Z][a-zA-Z-]*

literal-string: "\"" ~["] "\""

regex: "~"? character-class quantifier?

character-class: "[" character-class-content  "]"

character-class-content: (character-class-text | character-class-escaped-char)*

character-class-text: ~[\\\]]+

character-class-escaped-char: "\\" ~[\n]

quantifier: "?" | "+" | "*"

